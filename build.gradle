// Prerequisite:
// Current MATSim snapshot needs to be in the local Maven cache.
// (Put it there with 'mvn install' from the MATSim directory).

buildscript {
    // Dependencies of this build script (not of the project we are building):
    // A Gradle plug-in we will use later to download the JOSM binary.
    // Fetch it from Maven Central.
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'de.undercouch:gradle-download-task:1.0'
    }
}

// We use the Java builder and the download plug-in we just fetched.
apply plugin: "java"
apply plugin: 'download-task'

// For the dependencies of our project (not of this build script itself), we only need the
// local Maven cache. We use it to get the MATSim snapshot.
repositories {
    mavenLocal()
}

// Declare a configuration for our external dependency (MATSim) so we can later take its classes and zip them into our jar.
configurations {
    zipIntoJar
}

dependencies {
    zipIntoJar 'org.matsim:matsim:0.7.0-SNAPSHOT'
    // Compile is the distinguished configuration of the Java plug-in which actually needs to contain
    // the dependency.
    // We use zipIntoJar as a middleman, so that it has a name and we can later address its classes.
    // We add it to compile, so that it becomes a dependency of our project.
    compile configurations.zipIntoJar.dependencies
    // We also include the JOSM jar, which will be downloaded below. But it will not go into the jar.
    compile files("libs/josm-latest.jar")
}

jar.baseName = "matsim"
jar {
    metaInf {
        from("resources") {
            include "mime.types"
        }
    }
    // Put in the manifest entries which are required for a JOSM plug-in.
    manifest {
        attributes(
                "Plugin-Date": new Date().format("yyyy-MM-dd HH:mm"),
                "Plugin-Version": "v0.3.0",
                "Plugin-Mainversion": 7643,
                "Created-By": System.getProperty('java.version') + ' (' + System.getProperty('java.vendor') + ')',
                "Built-With": "gradle-${project.getGradle().getGradleVersion()}, groovy-${GroovySystem.getVersion()}",
                "Author" : "Nico Kuehnel",
                "Plugin-Class" : "org.matsim.contrib.josm.MATSimPlugin",
                "Plugin-Description": "Allows to edit and extract network information for the traffic simulation MATSim",
                "Plugin-Icon" : "images/dialogs/matsim-scenario.png",
                "Plugin-Link" : "http://www.matsim.org"
        )
    }
    // Now take the dependencies of zipIntoJar (currently only MATSim), and put them into
    // our target jar.
    from {
        configurations.zipIntoJar.collect { zipTree(it) }
    }
}

compileJava {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

// Download the JOSM binary and put it into its place.
project.ext.josmUrl = "http://josm.openstreetmap.de/josm-latest.jar"
task getJosm {
    description = "Download the latest JOSM build jar from the JOSM website"
    doLast {
        if (!file("libs").exists()) {
            file("libs").mkdir()
        }
        if (!file("libs/josm-latest.jar").exists()) {
            download {
                src project.ext.josmUrl
                dest 'libs'
            }
        }
    }
}
compileJava.dependsOn "getJosm"

sourceSets {
    rootDirectoryShit {
        resources {
            srcDir "."
            include "images/**/*.png"
        }
    }
    // Main is the designated source set name for the Java plug-in (just like in Maven).
    // It has designated sub-sets named Java and Resources (just like in Maven).
    // We leave Java at the default, but to Resources we add the images from the project root.
    main {
        resources {
            srcDir "src/main/resources"
            source rootDirectoryShit.resources
        }
    }
}

// Install the plug-in into the local JOSM plug-in directory.
// You can start JOSM from your IDE and debug the plug-in, but the JOSM
// plug-in system still expects the plug-in jar in its correct place.
//
// If you use Windows,
// please add something here which detects this and uses the proper JOSM
// plug-in path on Windows.
import org.apache.tools.ant.taskdefs.condition.Os
task installLocalJosm {
    doLast {
        def josmPluginDir;
        if (Os.isFamily(Os.FAMILY_WINDOWS)) {
            def appDataDir = System.getenv("APPDATA")
            josmPluginDir = appDataDir + "/JOSM/plugins"
        } else {
            def homePath = System.properties['user.home']
            josmPluginDir = homePath + "/.josm/plugins"
        }
        ant.copy(todir: josmPluginDir) {
            fileset(dir: "build/libs") {
                include(name: "matsim.jar")
            }
        }
    }
}
installLocalJosm.dependsOn "jar"

task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}

